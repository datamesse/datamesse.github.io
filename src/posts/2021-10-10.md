---
title: Dynamically apply time zone and daylight savings on date/times in Power BI
date: October 10, 2021
---

How to use Power Query to apply time zone offsets based on daylight savings "anchors" on date/times using a parameter, and without needing a separate calendar table in Power BI.

The final product is being able to use a Parameter to select a desired time zone, and apply it to your dataset's "Date/Time" column, and produce a "Date/Time/Zone" value.
![Power BI Tokyo example](https://raw.githubusercontent.com/datamesse/datamesse.github.io/main/src/assets-blog/2021-10-10--01.png?raw=true)

What makes this different from the DateTime.Zone function alone, is that this method respects when the UTC offset changes based on time zone by creating custom functions to do this.

This post incorporates my previous posts on [how to import time zone and daylight saving observations from Wikipedia](https://datamesse.github.io/#/post/1633183200), which indicate when different offsets are applied (e.g. first Sunday of October), and my post on [how to find the nth day of a month](https://datamesse.github.io/#/post/1632578400) to convert those into usable "anchors" to determine the offset value for the date/times.

**Note:** This is not an appropriate solution to time zone application in terms of data accuracy, processing efficiency, and coding involved. Ideally a predefined dataset or an API with actual date/time values for observation period start/ends would be best.

A good example of this can be found in [a blog post by John White](https://whitepages.unlimitedviz.com/2020/10/dynamic-time-zone-conversion-using-power-bi/)

This post shows how date/time anchor values can be used as an alternative way to solve this problem, which does not use calendar tables nor APIs.

This is the **[sample date/time dataset](https://raw.githubusercontent.com/datamesse/datamesse.github.io/main/src/assets-blog/Date_times_to_convert.xlsx?raw=true)** we will dynamically apply time zone offsets to via parameter selection.

Note there is no time zone in the data itself, so assumptions made by any application (e.g. user's machine time zone) may be incorrect.
![Sample date time dataset in Excel](https://raw.githubusercontent.com/datamesse/datamesse.github.io/main/src/assets-blog/2021-10-10--02.png?raw=true)

![Sample date time dataset imported into Power Query](https://raw.githubusercontent.com/datamesse/datamesse.github.io/main/src/assets-blog/2021-10-10--03.png?raw=true)

After importing the sample date/time dataset, we next import the combined time zone offset and daylight savings observation **[dataset](https://raw.githubusercontent.com/datamesse/datamesse.github.io/main/src/assets-blog/Time_zone_offsets_and_DST_observations.xlsx?raw=true)** created in this [post](https://datamesse.github.io/#/post/1633183200).

At this point, if you only need certain time zones to select from, you can filter for them here before proceeding.

![Offset and observation dataset imported into Power Query](https://raw.githubusercontent.com/datamesse/datamesse.github.io/main/src/assets-blog/2021-10-10--04.png?raw=true)

Next, we will create a list from the Timezone column, to be used as the available selections of the parameter.

![Power Query Convert to List Part A](https://raw.githubusercontent.com/datamesse/datamesse.github.io/main/src/assets-blog/2021-10-10--05.png?raw=true)

![Power Query Convert to List Part B](https://raw.githubusercontent.com/datamesse/datamesse.github.io/main/src/assets-blog/2021-10-10--06.png?raw=true)

Then set up the parameter itself to pull from that list.

![Power Query Create new parameter](https://raw.githubusercontent.com/datamesse/datamesse.github.io/main/src/assets-blog/2021-10-10--07.png?raw=true)

![Power Query Manage Parameters Part A](https://raw.githubusercontent.com/datamesse/datamesse.github.io/main/src/assets-blog/2021-10-10--08.png?raw=true)

![Power Query Manage Parameters Part B](https://raw.githubusercontent.com/datamesse/datamesse.github.io/main/src/assets-blog/2021-10-10--09.png?raw=true)

Next, we create a new column in the sample dataset whose value is the parameter selection.

![Power Query custom column for Parameter value Part A](https://raw.githubusercontent.com/datamesse/datamesse.github.io/main/src/assets-blog/2021-10-10--10.png?raw=true)

![Power Query custom column for Parameter value Part B](https://raw.githubusercontent.com/datamesse/datamesse.github.io/main/src/assets-blog/2021-10-10--11.png?raw=true)

Then merge the two datasets using that new custom column.
![Power Query Merge Queries](https://raw.githubusercontent.com/datamesse/datamesse.github.io/main/src/assets-blog/2021-10-10--12.png?raw=true)

![Power Query Merge Queries](https://raw.githubusercontent.com/datamesse/datamesse.github.io/main/src/assets-blog/2021-10-10--13.png?raw=true)

Whilst expanding the merged table, we can prefix the column names, which may be useful if intending to merge multiple times, e.g. parameter for data source's actual time zone vs parameter for desired time zone. We will only do the merge once, in this example.

![Power Query expand merge queries Part A](https://raw.githubusercontent.com/datamesse/datamesse.github.io/main/src/assets-blog/2021-10-10--14.png?raw=true)

![Power Query expand merge queries Part B](https://raw.githubusercontent.com/datamesse/datamesse.github.io/main/src/assets-blog/2021-10-10--15.png?raw=true)

Our next step is to create a custom column to convert our Date/Time value to match the time zone of the parameter selection. This is not as simple as appending an offset to our Date/Time value, because of these considerations

a. The need to account for different Date/Time offsets based on standard vs daylight savings.

b. The anchor dataset (which determines whether or not daylight savings is applied) has a mix of data structures e.g. anchor date/times can be either UTC or local time-based, and can either have a specific date of the month, or relative day position of the month.

c. The datasets' standard and daylight saving offset values are in a text based structure e.g. "+10:00", rather than straight numbers, which are more easily consumed by Power Query functions (e.g. *DateTime.AddZone()*).

Before we create the custom column, we will need 3 custom functions.
1. A simple suffix of the standard or daylight daylight offset to the Date/Time value to make it a DateTimeZone value, which we'll name **DatetimeToDatetimezone**.
2. A slightly more complex function that pulls in all date anchor values to convert to an actual date. But it only lets single parameters to pass for the time anchor (which could be UTC or local time) and offset (standard or daylight saving), which we'll name **AnchorToDatetimezone**.
3. The complex function that applies the time zone to the Date/Time value, factoring in daylight saving and standard time observation by using the previous two functions, which we'll name **DatetimeAppendZone**.

Beginning with the simple **DatetimeToDatetimezone** custom function, which is meant to resolve consideration *c)*.

![Power Query create custom function Part A](https://raw.githubusercontent.com/datamesse/datamesse.github.io/main/src/assets-blog/2021-10-10--16.png?raw=true)

![Power Query create custom function Part B](https://raw.githubusercontent.com/datamesse/datamesse.github.io/main/src/assets-blog/2021-10-10--17.png?raw=true)

```
let
  DatetimeToDatetimezone = (DateTimestamp as datetime, Offset as nullable text) => 
    DateTimeZone.FromText(DateTime.ToText(DateTimestamp) & " " & Offset)
in
  DatetimeToDatetimezone
```

![Power Query create 1st custom function](https://raw.githubusercontent.com/datamesse/datamesse.github.io/main/src/assets-blog/2021-10-10--18.png?raw=true)

Next we create the second custom function **AnchorToDatetimezone**, which takes in the dependent date/time value to get the year, and nullable parameters for time, offset, and the anchors for month, day, and *n*th occurrence of the day within the month. It checks the data structure to create the anchor's date, and appends the time and offset values passed into it.

* The first if condition checks that there are no incorrect nor incomplete data structures.
* The first else if condition creates the date time zone if there is a date anchor e.g. "21st" of given month.
* The second and third else if conditions convert to date time zones based on 1st, 2nd, 3rd, or 4th occurrence of specified day anchors, and are based on this previous [post](https://datamesse.github.io/#/post/1632578400).
* The fourth and fifth if else conditions convert based on the last occurrence of the specified day anchor, based on the last section of that same [post](https://datamesse.github.io/#/post/1632578400).

```
let
  AnchorToDatetimezone = (DateTimestamp as datetime, MonthAnchor as nullable number, DayAnchor as nullable number, PositionAnchor as nullable number, DateAnchor as nullable number, Time as nullable time, Offset as nullable text) => 
  /* Error-handling based on insufficient data or incorrect value combination */
  if (MonthAnchor = null) 
    or (DateAnchor = null and DayAnchor = null)
    or (DateAnchor <> null and DayAnchor <> null)
    or (DayAnchor <> null and PositionAnchor = null)
    or (DayAnchor = null and PositionAnchor <> null)
    or (Time = null)
    or (Offset = null)
  then "Incomplete data"
  /* Applying time zone to DateTimestamp, with separate conditions for position anchor = 9 i.e. "Last" */
  else if DateAnchor <> null
    then Text.From(Date.Year(DateTimestamp)) & "/" & Text.From(MonthAnchor)  & "/" & Text.From(DateAnchor) & " " & Text.From(Time) & Offset
  else if DayAnchor < 6 and PositionAnchor > 0 and PositionAnchor < 5
  /* Optional parameter in Date.DayOfWeek 1 = Day.Monday will get Sunday, hence DayAnchor (Sunday = 0) + 1 */
    then Text.From(Date.Year(DateTimestamp)) & "/" & Text.From(MonthAnchor) & "/" & Text.From( (7 - Date.DayOfWeek(Date.FromText(Text.From(Date.Year(DateTimestamp)) & "/" & Text.From(MonthAnchor) & "/1"), DayAnchor + 1)) + (-7 + (7 * PositionAnchor)) ) & " " & Text.From(Time) & Offset
  /* Need to pass Day.Sunday to get Saturday */
  else if DayAnchor = 6 and PositionAnchor > 0 and PositionAnchor < 5
    then Text.From(Date.Year(DateTimestamp)) & "/" & Text.From(MonthAnchor) & "/" & Text.From( (7 - Date.DayOfWeek(Date.FromText(Text.From(Date.Year(DateTimestamp)) & "/" & Text.From(MonthAnchor) & "/1"), Day.Sunday )) + (-7 + (7 * PositionAnchor)) ) & " " & Text.From(Time) & Offset
  /* handling for last specific day of month */
  else if DayAnchor = 0 and PositionAnchor = 9
    then Text.From(Date.AddDays(Date.EndOfMonth(Date.From(Text.From(Date.Year(DateTimestamp)) & "/" & Text.From(MonthAnchor) & "/1")),(-1 * Number.From(Date.DayOfWeek(Date.EndOfMonth(Date.From(Text.From(Date.Year(DateTimestamp)) & "/" & Text.From(MonthAnchor) & "/1")), Day.Sunday))))) & " " & Text.From(Time) & Offset
  else if DayAnchor > 0 and PositionAnchor = 9
    then Text.From(Date.AddDays(Date.EndOfMonth(Date.From(Text.From(Date.Year(DateTimestamp)) & "/" & Text.From(MonthAnchor) & "/1")),(-1 * (Number.From(Date.DayOfWeek(Date.EndOfMonth(Date.From(Text.From(Date.Year(DateTimestamp)) & "/" & Text.From(MonthAnchor) & "/1")), Day.Sunday)) + ( 7 - DayAnchor ))))) & " " & Text.From(Time) & Offset
  else null
in
  AnchorToDatetimezone
```

![Power Query create 2nd custom function](https://raw.githubusercontent.com/datamesse/datamesse.github.io/main/src/assets-blog/2021-10-10--19.png?raw=true)

The third custom function **DatetimeAppendZone** uses the **AnchorToDatetimezone** custom function to create date/time anchors for the start and end of daylight savings, then compares them with the Date/Time value to determine if standard or daylight savings offsets should be suffixed to the Date/Time value, using the **DatetimeToDatetimezone** custom function.

Some data sources may be incorrectly failing to account for daylight savings differences. In this third custom function, you can use the "Difference" parameter to add/subtract the missed hour(s) to compensate for those. The code below does not do that, but you can add it in, if required.

```
let
  DatetimeAppendZone = (DateTimestamp as datetime, Difference as number, StandardOffset as nullable text, DaylightOffset as nullable text, DSTstartAncDate as nullable number, DSTstartAncPosition as nullable number, DSTstartAncDay as nullable number, DSTstartAncMonth as nullable number, DSTstartAncUTC as nullable time, DSTstartAncLocal as nullable time, DSTendAncDate as nullable number, DSTendAncPosition as nullable number, DSTendAncDay as nullable number, DSTendAncMonth as nullable number, DSTendAncUTC as nullable time, DSTendAncLocal as nullable time) => 
  /* Validation to ensure same time anchor types for start and end are used */
  if Difference <> 0 and ( (DSTstartAncLocal = null and DSTstartAncUTC = null) or (DSTendAncLocal = null and DSTendAncUTC = null) )
    then "Incomplete data"

  /* Where DST is not observed, just append Standard UTC offset */
  else if Difference = 0
    then DatetimeToDatetimezone(DateTimestamp, StandardOffset)

  /* From this point on, if using a data source that doesn't properly account for Daylight Savings offsets, you can factor those into the calculations */

  /* Where DST is observed with Standard time result */
  else if Difference <> 0
    and (
          (
           /* Where local offset is used, 1 DST period in same year, datetimestamp is outside daylight savings */
           DSTstartAncLocal <> null and DSTstartAncMonth < DSTendAncMonth
           and ( DatetimeToDatetimezone(DateTimestamp,StandardOffset) < DateTimeZone.From(AnchorToDatetimezone(DateTimestamp, DSTstartAncMonth, DSTstartAncDay, DSTstartAncPosition, DSTstartAncDate, DSTstartAncLocal, StandardOffset))
                or DatetimeToDatetimezone(DateTimestamp,StandardOffset) > DateTimeZone.From(AnchorToDatetimezone(DateTimestamp, DSTendAncMonth, DSTendAncDay, DSTendAncPosition, DSTendAncDate, DSTendAncLocal, StandardOffset)) )
          )
      or  (
           /* Where local offset is used, 2 DST periods in same year, datetimestamp is outside both daylight savings periods */
           DSTstartAncLocal <> null and DSTstartAncMonth > DSTendAncMonth 
           and Date.Month(DateTimestamp) >= DSTendAncMonth and Date.Month(DateTimestamp) <= DSTstartAncMonth
           and DatetimeToDatetimezone(DateTimestamp,DaylightOffset) > DateTimeZone.From(AnchorToDatetimezone(DateTimestamp, DSTendAncMonth, DSTendAncDay, DSTendAncPosition, DSTendAncDate, DSTendAncLocal, DaylightOffset))
           and DatetimeToDatetimezone(DateTimestamp,DaylightOffset) < DateTimeZone.From(AnchorToDatetimezone(Date.AddYears(DateTimestamp,1), DSTstartAncMonth, DSTstartAncDay, DSTstartAncPosition, DSTstartAncDate, DSTstartAncLocal, DaylightOffset))           
          )
      or  (
           /* Where UTC offset is used, 1 DST period in same year, datetimestamp is inside daylight savings */
           DSTstartAncUTC <> null and DSTstartAncMonth < DSTendAncMonth
           and ( DateTimeZone.ToUtc(DatetimeToDatetimezone(DateTimestamp,StandardOffset)) <  DateTimeZone.FromText(AnchorToDatetimezone(DateTimestamp, DSTstartAncMonth, DSTstartAncDay, DSTstartAncPosition, DSTstartAncDate, DSTstartAncUTC, "+00:00"))
                 or DateTimeZone.ToUtc(DatetimeToDatetimezone(DateTimestamp,StandardOffset)) > DateTimeZone.FromText(AnchorToDatetimezone(DateTimestamp, DSTendAncMonth, DSTendAncDay, DSTendAncPosition, DSTendAncDate, DSTendAncUTC, "+00:00")) )
          )
    )
    then DatetimeToDatetimezone(DateTimestamp, StandardOffset)

  /* Where DST is observed with Dayliht Saving time result */
  else if Difference <> 0
    and (
          (
           /* Where local offset is used, 1 DST period within same year, datetimestamp is inside daylight savings */    
           DSTstartAncLocal <> null and DSTstartAncMonth < DSTendAncMonth
           and ( DatetimeToDatetimezone(DateTimestamp,StandardOffset) >= DateTimeZone.From(AnchorToDatetimezone(DateTimestamp, DSTstartAncMonth, DSTstartAncDay, DSTstartAncPosition, DSTstartAncDate, DSTstartAncLocal, StandardOffset))
                or DatetimeToDatetimezone(DateTimestamp,StandardOffset) <= DateTimeZone.From(AnchorToDatetimezone(DateTimestamp, DSTendAncMonth, DSTendAncDay, DSTendAncPosition, DSTendAncDate, DSTendAncLocal, StandardOffset)) )
          )
      or  (
           /* Where local offset is used, 2 DST periods in same year, datetimestamp is inside 1st daylight savings period */
           DSTstartAncLocal <> null and DSTstartAncMonth > DSTendAncMonth and Date.Month(DateTimestamp) <= DSTendAncMonth
           and DatetimeToDatetimezone(DateTimestamp,DaylightOffset) <= DateTimeZone.From(AnchorToDatetimezone(DateTimestamp, DSTendAncMonth, DSTendAncDay, DSTendAncPosition, DSTendAncDate, DSTendAncLocal, DaylightOffset))
          )
      or  (
           /* Where local offset is used, 2 DST periods in same year, datetimestamp is inside 2nd daylight savings period */
           DSTstartAncLocal <> null and DSTstartAncMonth > DSTendAncMonth and Date.Month(DateTimestamp) >= DSTendAncMonth
           and DatetimeToDatetimezone(DateTimestamp,DaylightOffset) >= DateTimeZone.From(AnchorToDatetimezone(DateTimestamp, DSTstartAncMonth, DSTstartAncDay, DSTstartAncPosition, DSTstartAncDate, DSTstartAncLocal, DaylightOffset))
          )
      or  (
           /* Where UTC offset is used, 1 DST period in same year, datetimestamp is outside daylight savings */
           DSTstartAncUTC <> null and DSTstartAncMonth < DSTendAncMonth
           and DateTimeZone.ToUtc(DatetimeToDatetimezone(DateTimestamp,StandardOffset)) >=  DateTimeZone.From(AnchorToDatetimezone(DateTimestamp, DSTstartAncMonth, DSTstartAncDay, DSTstartAncPosition, DSTstartAncDate, DSTstartAncUTC, "+00:00"))
           and DateTimeZone.ToUtc(DatetimeToDatetimezone(DateTimestamp,StandardOffset)) <= DateTimeZone.From(AnchorToDatetimezone(DateTimestamp, DSTendAncMonth, DSTendAncDay, DSTendAncPosition, DSTendAncDate, DSTendAncUTC, "+00:00")) 
          )
    )
    then DatetimeToDatetimezone(DateTimestamp, DaylightOffset)
  else null
in
  DatetimeAppendZone
```

![Power Query create 3rd custom function](https://raw.githubusercontent.com/datamesse/datamesse.github.io/main/src/assets-blog/2021-10-10--20.png?raw=true)

Now that our functions are complete, we can use the third **DatetimeAppendZone** to create the custom column which converts Date/Time to a datetimezone value, by passing in all the respective anchors as parameters.

```
DatetimeAppendZone([#"Date/Time"],[#"Daylight offset - Standard offset"],[Standard UTC offset],[Daylight Saving UTC offset],[#"DST start (date anchor)"],[#"DST start (position anchor)"],[#"DST start (day anchor)"],[#"DST start (month anchor)"],[#"DST start (UTC time anchor)"],[#"DST start (local time anchor)"],[#"DST end (date anchor)"],[#"DST end (position anchor)"],[#"DST end (day anchor)"],[#"DST end (month anchor)"],[#"DST end (UTC time anchor)"],[#"DST end (local time anchor)"])
```

![Power BI Create report](https://raw.githubusercontent.com/datamesse/datamesse.github.io/main/src/assets-blog/2021-10-10--21.png?raw=true)

If we now create a table in the Power BI's report designer, we can see the appropriate suffixing of standard vs daylight saving offsets to our Date/Time values as we change the parameter. Then we can test to see if, regardless of the data structure used for the daylight savings anchoring, that the appropriate offset is applied to our Date/Time dataset.

Australia, Sydney is a time zone that uses local time and non-"last position" for its daylight savings start and end anchors.
![Power BI Sydney example Part A](https://raw.githubusercontent.com/datamesse/datamesse.github.io/main/src/assets-blog/2021-10-10--22a.png?raw=true)

![Power BI Sydney example Part B](https://raw.githubusercontent.com/datamesse/datamesse.github.io/main/src/assets-blog/2021-10-10--22.png?raw=true)

Europe, Dublin is a time zone that uses UTC time and "last position" (indicated by the 9), for its anchors.
![Power BI Dublin example Part A](https://raw.githubusercontent.com/datamesse/datamesse.github.io/main/src/assets-blog/2021-10-10--23a.png?raw=true)

![Power BI Dublin example Part B](https://raw.githubusercontent.com/datamesse/datamesse.github.io/main/src/assets-blog/2021-10-10--23.png?raw=true)

Africa, Casablanca is a time zone that uses a fixed date of the month for its anchors.
![Power BI Casablanca example Part A](https://raw.githubusercontent.com/datamesse/datamesse.github.io/main/src/assets-blog/2021-10-10--24a.png?raw=true)

![Power BI Casablanca example Part B](https://raw.githubusercontent.com/datamesse/datamesse.github.io/main/src/assets-blog/2021-10-10--24.png?raw=true)

So now we have created a parameterised way of defining our data source's time zone, with potential to compensate for skipped daylight savings conversions it may have, by using the concept of anchor values to construct relative date/times.

Use cases for this include:
1. Dynamically converting date/times to another time zone e.g. a report reader from one time zone wanting to know the date/times from the perspective of another time zone.
2. The admittedly rare instance where the time zone of date/times may vary at access or export when creating a datset.

An example where I encountered use case #2, was manually exporting data from Explore, the reporting tool for the Zendesk customer service platform, where date/times are automatically converted to match the time zone of the extractor's Zendesk login.

In that scenario, if multiple people from different time zones are creating or maintaining dashboards not made in the native Explore tool (e.g. via Power BI), their extract date times can be inconsistent.

There are simple ways around this:
* Creating a shared account fixed to a specific time zone for data extracts.
* Having access to the data source's API.

You can take this further for other solutions, such as hard-coding the desired time zone, or make it based on the values of another column e.g. time zone is based on country or city values.


Click **[here](https://github.com/datamesse/datamesse.github.io/blob/main/src/posts/2021-10-10.md)** for this post's markdown file in GitHub.